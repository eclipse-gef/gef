/*******************************************************************************
 * Copyright (c) 2016, 2018 itemis AG and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     Alexander Ny√üen (itemis AG) - initial API and implementation
 *     Tamas Miklossy  (itemis AG) - major refactorings (bug #477980)
 *                                 - support for empty arrowtype (bug #530990)
 *******************************************************************************/
grammar org.eclipse.gef.dot.internal.language.DotArrowType

generate arrowtype "http://www.eclipse.org/gef/dot/internal/language/DotArrowType"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// XXX: While not documented explicitly, an empty arrowtype seems to be valid as well (see bug #530990)
ArrowType:
	{ArrowType}
	(arrowShapes+=AbstractArrowShape (arrowShapes+=AbstractArrowShape (arrowShapes+=AbstractArrowShape
	(arrowShapes+=AbstractArrowShape)?)?)?)?;

AbstractArrowShape:
	ArrowShape | DeprecatedArrowShape;

ArrowShape:
	(open?="o")? (side=("l" | "r"))? shape=PrimitiveShape;

DeprecatedArrowShape:
	shape = DeprecatedShape;

enum DeprecatedShape:
	ediamond="ediamond" | open="open" | halfopen="halfopen" | empty="empty" | invempty="invempty";

enum PrimitiveShape:
	box="box" | crow="crow" | curve="curve" | icurve="icurve" | diamond="diamond" | dot="dot" | inv="inv" | none="none" |
	normal="normal" | tee="tee" | vee="vee";
