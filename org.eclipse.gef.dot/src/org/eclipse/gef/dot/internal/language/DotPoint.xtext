/*******************************************************************************
 * Copyright (c) 2016, 2020 itemis AG and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     Alexander Ny√üen (itemis AG) - initial API and implementation
 *     Zoey Prigge     (itemis AG) - include exp and hex in double literal grammar (bug #559031)
 *
 *******************************************************************************/
grammar org.eclipse.gef.dot.internal.language.DotPoint hidden(WS)

generate point "http://www.eclipse.org/gef/dot/internal/language/DotPoint"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Point:
	x=DOUBLE "," y=DOUBLE ("," z=DOUBLE)? (inputOnly?="!")?;

terminal DOUBLE returns ecore::EDouble:
	//negative numbers
	('-')?
	(
		// decimal numbers
		((('.' ('0'..'9')+) | (('0'..'9')+ (('.') ('0'..'9')*)?)) (('e'|'E') ('+'|'-')? ('0'..'9')+)?) |
		// hexadecimal numbers
		('0' ('x'|'X')
			(('.' ('0'..'9'|'a'..'f'|'A'..'F')+) | (('0'..'9'|'a'..'f'|'A'..'F')+ (('.') ('0'..'9'|'a'..'f'|'A'..'F')*)?))
			(('p'|'P') ('+'|'-')? ('0'..'9')+)?
		)
	);

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;